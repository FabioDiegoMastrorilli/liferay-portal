definition {

	macro assertExecutionEntry {
		AssertElementPresent(
			key_entityType = "${entityType}",
			key_executionAction = "${executionAction}",
			key_executionStatus = "${executionStatus}",
			locator1 = "ImportExport#EXECUTION_ENTRY");
	}

	macro gotoExport {
		Click(locator1 = "Button#PLUS");

		MenuItem.click(menuItem = "Export File");
	}

	macro gotoImport {
		Click(locator1 = "Button#PLUS");

		MenuItem.click(menuItem = "Import File");
	}

	macro mapExport {
		Click.clickAtNotVisible(
			key_checkboxOption = "Attribute Code",
			locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");

		for (var exportField : list "${exportFields}") {
			Click.clickAtNotVisible(
				key_checkboxOption = "${exportField}",
				locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
		}
	}

	macro mapImport {
		for (var fieldMapping : list "${fieldMappings}") {
			var destinationField = StringUtil.extractFirst("${fieldMapping}", ":");
			var sourceField = StringUtil.extractLast("${fieldMapping}", ":");

			echo("Mapping ${destinationField} to ${sourceField} source field");

			Select(
				key_destinationField = "${destinationField}",
				locator1 = "ImportExport#IMPORT_MAPPING_SELECT",
				value1 = "${sourceField}");
		}
	}

	macro openImportExportAdmin {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/guest/~/control_panel/manage?p_p_id=com_liferay_batch_planner_web_internal_portlet_BatchPlannerPortlet");

		WaitForVisible(locator1 = "Button#PLUS");
	}

	macro selectEntity {
		Select(
			key_selectFieldLabel = "Headless Endpoint",
			locator1 = "ImportExport#SELECT",
			value1 = "${headlessEndpoint}");

		WaitForElementPresent(
			key_selectFieldLabel = "Entity",
			locator1 = "ImportExport#SELECT");

		if (IsElementPresent(key_selectFieldLabel = "Entity Type", locator1 = "ImportExport#SELECT")) {
			Select(
				key_selectFieldLabel = "Entity Type",
				locator1 = "ImportExport#SELECT",
				value1 = "${entityType}");
		}
		else {
			Select(
				key_selectFieldLabel = "Entity Name",
				locator1 = "ImportExport#SELECT",
				value1 = "${entityType}");
		}
	}

}